include "iwbtg/src/globals"


define LEVEL_OBJ_COUNT_INDEX 2

define SCREEN_WIDTH_TILES 20
define SCREEN_HEIGHT_TILES 15


fn load_levels() {
    let level_data_pointer = LEVEL_DATA
    let amount_levels = get(level_data_pointer)
    level_data_pointer = level_data_pointer + 1

    for (let i = 0; i < amount_levels; i = i + 1) {
        set(levels_list+i, level_data_pointer)
        level_data_pointer = level_data_pointer + LEVEL_OBJ_COUNT_INDEX
        let level_object_count = get(level_data_pointer)
        level_data_pointer = level_data_pointer + level_object_count * 5 + 1
    }
}


fn draw_level_object(obj_id, obj_x, obj_y, obj_width, obj_height) {
    let obj_texture_x = obj_id * TILE_SIZE

    for (let i = 0; i < obj_height; i = i + 1) {
        for (let j = 0; j < obj_width; j = j + 1) {
            let sub_obj_x = j + obj_x
            let sub_obj_y = i + obj_y

            // Don't draw objects outside of the screen
            if (sub_obj_x > SCREEN_WIDTH_TILES-1) + (sub_obj_x < 0) + 
                (sub_obj_y > SCREEN_HEIGHT_TILES-1) + (sub_obj_y < 0) {
                continue
            }

            draw_texture_region(SS_TILES,
                sub_obj_x * TILE_SIZE, sub_obj_y * TILE_SIZE,
                obj_texture_x, 0, TILE_SIZE, TILE_SIZE,
                false
            )
        }
    }
    // Draw debug object outline
    // color(0, 255, 0)
    // let debug_x = obj_x * TILE_SIZE
    // let debug_y = obj_y * TILE_SIZE
    // let debug_right = debug_x + obj_width * TILE_SIZE
    // let debug_bottom = debug_y + obj_height * TILE_SIZE
    // line(debug_x, debug_y, debug_right, debug_y)
    // line(debug_x, debug_bottom, debug_right, debug_bottom)
    // line(debug_x, debug_y, debug_x, debug_bottom)
    // line(debug_right, debug_y, debug_right, debug_bottom)
}


fn draw_current_level() {
    // Draw background
    color(90, 150, 240)
    rect(0, 0, WIDTH, HEIGHT)
    
    // Draw objects
    for (let i = 0; i < current_level_obj_count; i = i + 1) {
        let obj_id = get(obj_id_array+i)
        let obj_x = get(obj_x_array+i) / (COORD_SCALE * TILE_SIZE)
        let obj_y = get(obj_y_array+i) / (COORD_SCALE * TILE_SIZE)
        let obj_width = get(obj_width_array+i) / (COORD_SCALE * TILE_SIZE)
        let obj_height = get(obj_height_array+i) / (COORD_SCALE * TILE_SIZE)
        draw_level_object(obj_id, obj_x, obj_y, obj_width, obj_height)
    }
}


fn load_current_level(direction) {
    let level_data_pointer = get(levels_list+current_level)

    // Delete bullets
    set(bullet_directions, 0)
    set(bullet_directions+1, 0)
    set(bullet_directions+2, 0)
    set(bullet_directions+3, 0)
    bullet_count = 0
    
    if !(direction == -1) {
        if direction == ENUM_UP {
            kid_y = HEIGHT * COORD_SCALE - KID_HEIGHT
        }
        else if direction == ENUM_RIGHT {
            kid_x = 0
        }
        else if direction == ENUM_DOWN {
            kid_y = 0
        }
        else {
            kid_x = WIDTH * COORD_SCALE - KID_WIDTH
        }
    }

    prev_level_direction = get(level_data_pointer)
    next_level_direction = get(level_data_pointer+1)

    level_data_pointer = level_data_pointer + LEVEL_OBJ_COUNT_INDEX
    current_level_obj_count = get(level_data_pointer)
    level_data_pointer = level_data_pointer + 1
    for (let i = 0; i < current_level_obj_count; i = i + 1) {
        let obj_id = get(level_data_pointer)
        let obj_x = get(level_data_pointer+1)
        let obj_y = get(level_data_pointer+2)
        obj_x = TILE_SIZE * COORD_SCALE * (obj_x - 256 * (obj_x > 127))
        obj_y = TILE_SIZE * COORD_SCALE * (obj_y - 256 * (obj_y > 127))
        let obj_width = TILE_SIZE * COORD_SCALE * get(level_data_pointer+3)
        let obj_height = TILE_SIZE * COORD_SCALE * get(level_data_pointer+4)

        set(obj_id_array+i, obj_id)
        set(obj_x_array+i, obj_x)
        set(obj_y_array+i, obj_y)
        set(obj_width_array+i, obj_width)
        set(obj_height_array+i, obj_height)
        level_data_pointer = level_data_pointer + 5
    }

    draw_current_level()

    // Set this to zero to avoid drawing save block on new screen
    hit_save_block_timer = 0
}