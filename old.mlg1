fn kid_intersect_triangle(tri_top_left_x, tri_top_left_y, tri_direction) {
    let tri_size = TILE_SIZE * COORD_SCALE
    let half_size = tri_size / 2

    let t1_x = 0
    let t1_y = 0
    let t2_x = 0
    let t2_y = 0
    let t3_x = 0
    let t3_y = 0

    // Up
    if tri_direction == 0 {
        t1_x = tri_top_left_x + half_size
        t1_y = tri_top_left_y
        t2_x = tri_top_left_x
        t2_y = tri_top_left_y - tri_size
        t3_x = tri_top_left_x + tri_size
        t3_y = tri_top_left_y - tri_size
    }
    // Right
    else if tri_direction == 1 {
        t1_x = tri_top_left_x + tri_size
        t1_y = tri_top_left_y - half_size
        t2_x = tri_top_left_x
        t2_y = tri_top_left_y
        t3_x = tri_top_left_x
        t3_y = tri_top_left_y - tri_size
    }
    // Down
    else if tri_direction == 2 {
        t1_x = tri_top_left_x + half_size
        t1_y = tri_top_left_y - tri_size
        t2_x = tri_top_left_x
        t2_y = tri_top_left_y
        t3_x = tri_top_left_x + tri_size
        t3_y = tri_top_left_y
    }
    // Left
    else {
        t1_x = tri_top_left_x
        t1_y = tri_top_left_y - half_size
        t2_x = tri_top_left_x + tri_size
        t2_y = tri_top_left_y
        t3_x = tri_top_left_x + tri_size
        t3_y = tri_top_left_y - tri_size
    }

    let kid_right = kid_x + KID_WIDTH
    let kid_bottom = kid_y + KID_HEIGHT
    let tri_vertices[6] = [t1_x, t1_y, t2_x, t2_y, t3_x, t3_y]
    let rect_corners[8] = [
        kid_x, kid_y, 
        kid_right, kid_y,
        kid_right, kid_bottom,
        kid_x, kid_bottom
    ]

    let i = 0
    for (i = 0; i < 6; i = i + 2) {
        let tx = get(tri_vertices+i)
        let ty = get(tri_vertices+i+1)
        if (tx > kid_x) * (tx < kid_right) * (ty > kid_y) * (ty < kid_bottom) {
            return true
        }
    }

    let denom = (t2_y - t3_y) * (t1_x - t3_x) + (t3_x - t2_x) * (t1_y - t3_y)
    for (i = 0; i < 8; i = i + 2) {
        let cx = get(rect_corners+i)
        let cy = get(rect_corners+i+1)
        let a = ((t2_y - t3_y) * (cx - t3_x) + (t3_x - t2_x) * (cy - t3_y)) / denom
        let b = ((t3_y - t1_y) * (cx - t3_x) + (t1_x - t3_x) * (cy - t3_y)) / denom
        let c = 1 - a - b
        if !((a < 0) + (b < 0) + (c < 0)) {
            return true
        }
    }

    return false
}